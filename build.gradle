plugins {
    id 'java'
    id 'application'
    id 'org.springframework.boot' version '3.2.5'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'org.beryx.runtime' version '1.13.1'
    id 'jacoco'
}

group = 'de.zeltlager.kuechenplaner'
version = '0.1.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

application {
    mainClass = 'de.zeltlager.kuechenplaner.App'
}

bootJar {
    enabled = false
}

jar {
    enabled = true
}

runtime {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']

    jpackage {
        imageName = 'Kuechenplaner'
        installerName = "Kuechenplaner-${project.version}"
        installerType = 'exe'
        appVersion = project.version
        installerOptions += ['--win-menu', '--win-shortcut']
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.flywaydb:flyway-core'

    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'com.mysql:mysql-connector-j'
    runtimeOnly 'org.postgresql:postgresql'

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.mockito:mockito-core:5.11.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.11.0'
    testImplementation 'org.assertj:assertj-core:3.25.3'
}

test {
    useJUnitPlatform()
}

def coverageExclusions = [
        'de/zeltlager/kuechenplaner/App*.class',
        'de/zeltlager/kuechenplaner/MainWindow*.class',
        'de/zeltlager/kuechenplaner/*Panel*.class',
        'de/zeltlager/kuechenplaner/*TableModel*.class',
        'de/zeltlager/kuechenplaner/RecipeDetailDialog*.class',
        'de/zeltlager/kuechenplaner/gui/**',
        'de/zeltlager/kuechenplaner/ui/**',
        'de/zeltlager/kuechenplaner/config/**'
]

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        csv.required = false
        html.required = true
    }
    classDirectories.setFrom(files(classDirectories.files.collect {
        fileTree(dir: it, exclude: coverageExclusions)
    }))
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport
    classDirectories.setFrom(files(classDirectories.files.collect {
        fileTree(dir: it, exclude: coverageExclusions)
    }))
    violationRules {
        rule {
            limit {
                minimum = 0.7
            }
        }
    }
}

check.dependsOn jacocoTestCoverageVerification
